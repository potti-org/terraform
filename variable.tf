#
# Sensitive Provider Credentials
#

variable "ovh_app_key" {
  description = "OVH API application key"
  type        = string
  sensitive   = true
}

variable "ovh_app_secret" {
  description = "OVH API application secret"
  type        = string
  sensitive   = true
}

variable "ovh_app_consumer_key" {
  description = "OVH API consumer key"
  type        = string
  sensitive   = true
}

variable "os_username" {
  description = "OpenStack username"
  type        = string
  sensitive   = true
}

variable "os_password" {
  description = "OpenStack password"
  type        = string
  sensitive   = true
}

variable "os_tenant_id" {
  description = "OpenStack tenant/project ID"
  type        = string
}

variable "os_tenant_name" {
  description = "OpenStack tenant/project name"
  type        = string
}

variable "cloudflare_api_token" {
  description = "Cloudflare API token"
  type        = string
  sensitive   = true
}

#
# SSH Keys
#

variable "instance_ssh_public_key" {
  description = "SSH public key for compute instances (generated by bastion)"
  type        = string
}

variable "ssh_public_key" {
  description = "Personal SSH public key for bastion access"
  type        = string
}

#
# Region Configuration
#

variable "regions" {
  description = "Public Cloud regions with their availability zones"
  type = map(object({
    availability_zones = list(string)
  }))
  default = {
    "EU-WEST-PAR" = {
      availability_zones = ["EU-WEST-PAR-A", "EU-WEST-PAR-B"]
    }
  }
}

#
# Network Configuration
#

variable "private_network_potti_par" {
  description = "Private network on vrack, only on region EU-WEST-PAR"
  type = object({
    vlanid     = number
    dhcp       = bool
    cidr       = string
    no_gateway = bool
  })
  default = {
    vlanid     = 101
    dhcp       = true
    cidr       = "10.101.0.0/16"
    no_gateway = false
  }

  validation {
    condition     = can(cidrnetmask(var.private_network_potti_par.cidr))
    error_message = "The cidr must be a valid CIDR notation (e.g., 10.0.0.0/16)."
  }

  validation {
    condition     = var.private_network_potti_par.vlanid >= 1 && var.private_network_potti_par.vlanid <= 4094
    error_message = "VLAN ID must be between 1 and 4094."
  }
}

#
# Server Configuration
#

variable "app_server" {
  description = "Application server configuration"
  type = object({
    image  = string
    flavor = string
    count  = number
    name   = string
  })
  default = {
    image  = "Ubuntu 24.04"
    flavor = "b3-8"
    count  = 1
    name   = "potti_app_server"
  }

  validation {
    condition     = var.app_server.count >= 1 && var.app_server.count <= 10
    error_message = "App server count must be between 1 and 10."
  }
}

variable "bastion_server" {
  description = "Bastion server configuration"
  type = object({
    image      = string
    flavor     = string
    count      = number
    name       = string
    region     = string
    ip_address = string
  })
  default = {
    image      = "Ubuntu 24.04"
    flavor     = "b3-8"
    count      = 1
    name       = "potti_bastion"
    region     = "EU-WEST-PAR-C"
    ip_address = "10.101.3.50"
  }
}

variable "ci_server" {
  description = "CI server configuration"
  type = object({
    image      = string
    flavor     = string
    name       = string
    region     = string
    ip_address = string
  })
  default = {
    image      = "Ubuntu 24.04"
    flavor     = "c3-16"
    name       = "potti_ci"
    region     = "EU-WEST-PAR-C"
    ip_address = "10.101.3.60"
  }
}

#
# Load Balancer Configuration
#

variable "load_balancer_config" {
  description = "Load balancer configuration"
  type = object({
    health_check_domain = string
  })
  default = {
    health_check_domain = "staging.potti.fr"
  }
}

#
# S3 Configuration
#

variable "s3_bucket_config" {
  description = "S3 bucket configuration including replication settings"
  type = object({
    name                       = string
    versioning_enabled         = bool
    encryption_algorithm       = string
    user_actions               = list(string)
    replication_enabled        = bool
    replication_region         = optional(string)
    replication_storage_class  = optional(string)
    replication_prefix         = optional(string)
    replicate_delete_markers   = optional(bool)
    remove_replica_on_deletion = optional(bool)
  })
  default = {
    name                       = "potti-bucket"
    versioning_enabled         = true
    encryption_algorithm       = "AES256"
    user_actions               = ["s3:*"]
    replication_enabled        = true
    replication_region         = "GRA"
    replication_storage_class  = "STANDARD_IA"
    replication_prefix         = null
    replicate_delete_markers   = true
    remove_replica_on_deletion = false
  }

  validation {
    condition     = var.s3_bucket_config.encryption_algorithm == "AES256" || var.s3_bucket_config.encryption_algorithm == "aws:kms"
    error_message = "Encryption algorithm must be 'AES256' or 'aws:kms'."
  }

  validation {
    condition     = !var.s3_bucket_config.replication_enabled || (var.s3_bucket_config.replication_enabled && var.s3_bucket_config.replication_region != null)
    error_message = "replication_region is required when replication_enabled is true."
  }

  validation {
    condition = !var.s3_bucket_config.replication_enabled || contains([
      "HIGH_PERF", "STANDARD", "STANDARD_IA"
    ], coalesce(var.s3_bucket_config.replication_storage_class, "STANDARD"))
    error_message = "replication_storage_class must be a valid S3 storage class."
  }
}

variable "s3_cors_rules" {
  description = "CORS rules for the S3 bucket"
  type = list(object({
    AllowedHeaders = list(string)
    AllowedMethods = list(string)
    AllowedOrigins = list(string)
  }))
  default = [
    {
      AllowedMethods = ["GET"]
      AllowedOrigins = ["*"]
      AllowedHeaders = ["*"]
    },
    {
      AllowedMethods = ["PUT", "POST"]
      AllowedOrigins = ["https://potti.co", "https://staging.potti.co"]
      AllowedHeaders = ["*"]
    }
  ]
}

#
# Database Configuration
#

variable "postgres_config" {
  description = "PostgreSQL database configuration"
  type = object({
    description         = string
    version             = string
    plan                = string
    flavor              = string
    backup_time         = string
    maintenance_time    = string
    backup_regions      = list(string)
    deletion_protection = bool
    user_name           = string
  })
  default = {
    description         = "potti_postgres"
    version             = "18"
    plan                = "production"
    flavor              = "b3-8"
    backup_time         = "02:00:00"
    maintenance_time    = "03:00:00"
    backup_regions      = ["EU-WEST-PAR", "GRA"]
    deletion_protection = true
    user_name           = "potti_production"
  }

  validation {
    condition     = can(regex("^[0-9]{2}:[0-9]{2}:[0-9]{2}$", var.postgres_config.backup_time))
    error_message = "backup_time must be in HH:MM:SS format."
  }

  validation {
    condition     = can(regex("^[0-9]{2}:[0-9]{2}:[0-9]{2}$", var.postgres_config.maintenance_time))
    error_message = "maintenance_time must be in HH:MM:SS format."
  }
}

#
# Cache Configuration
#

variable "valkey_config" {
  description = "Valkey cache configuration"
  type = object({
    description         = string
    version             = string
    plan                = string
    flavor              = string
    backup_time         = string
    maintenance_time    = string
    backup_regions      = list(string)
    deletion_protection = bool
    user_name           = string
  })
  default = {
    description         = "potti_cache"
    version             = "8.1"
    plan                = "production"
    flavor              = "b3-8"
    backup_time         = "02:00:00"
    maintenance_time    = "03:00:00"
    backup_regions      = ["EU-WEST-PAR", "GRA"]
    deletion_protection = true
    user_name           = "potti_cache_user"
  }

  validation {
    condition     = can(regex("^[0-9]{2}:[0-9]{2}:[0-9]{2}$", var.valkey_config.backup_time))
    error_message = "backup_time must be in HH:MM:SS format."
  }

  validation {
    condition     = can(regex("^[0-9]{2}:[0-9]{2}:[0-9]{2}$", var.valkey_config.maintenance_time))
    error_message = "maintenance_time must be in HH:MM:SS format."
  }
}
